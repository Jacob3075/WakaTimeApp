plugins {
    id("com.android.application")
    id("kotlin-android")
    id("org.jetbrains.kotlin.kapt")
    id("dagger.hilt.android.plugin")
    id("com.google.devtools.ksp")

    id("wakatimeapp.detekt")
}

android {
    compileSdk = 33

    defaultConfig {
        minSdk = 26
        targetSdk = 33

        applicationId = "com.jacob.wakatimeapp"
        versionCode = 1
        versionName = "1.0.0"

        testInstrumentationRunner = "com.jacob.wakatimeapp.HiltTestRunner"
        vectorDrawables { useSupportLibrary = true }

        manifestPlaceholders["appAuthRedirectScheme"] = "wakatimeapp"
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions { jvmTarget = "11" }

    buildFeatures { compose = true }
    composeOptions { kotlinCompilerExtensionVersion = "1.3.1" }

    // Compose Destination config
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

dependencies {
    // Androidx Core
    implementation("androidx.core:core-ktx:1.9.0")
    implementation("androidx.appcompat:appcompat:1.5.1")
    implementation("androidx.legacy:legacy-support-v4:1.0.0")

    // Compose
    implementation("androidx.compose.ui:ui:1.2.1")
    implementation("androidx.compose.material:material:1.2.1")
    implementation("androidx.compose.ui:ui-tooling-preview:1.2.1")
    implementation("androidx.activity:activity-compose:1.5.1")

    // Navigation
    implementation("io.github.raamcosta.compose-destinations:core:1.6.19-beta")
    ksp("io.github.raamcosta.compose-destinations:ksp:1.6.19-beta")

    implementation("androidx.hilt:hilt-navigation-fragment:1.0.0")

    // Hilt
    implementation("com.google.dagger:hilt-android:2.43.1")
    kapt("com.google.dagger:hilt-android-compiler:2.43.1")

    // Logging
    implementation("com.jakewharton.timber:timber:5.0.1")

    detektPlugins("ru.kode:detekt-rules-compose:1.2.1")
    detektPlugins("com.twitter.compose.rules:detekt:0.0.14")
}

kapt {
    correctErrorTypes = true
}
