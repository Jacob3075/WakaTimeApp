plugins {
    id "io.gitlab.arturbosch.detekt"
    id "com.github.ben-manes.versions"
}

dependencies {
    detektPlugins(libs.detekt) // https://github.com/twitter/compose-rules/
    detektPlugins(libs.detekt.formatting) // https://detekt.dev
}

detekt {
    parallel = true
    config = files("${rootProject.rootDir}/tools/detekt-config.yml")
    buildUponDefaultConfig = true
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    jvmTarget = "11"
    reports {
        html.required.set(false)
        md.required.set(true)
        md.outputLocation.set(file("${rootProject.rootDir}/tools/reports/${project.name}-detekt.md"))
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }

    gradleReleaseChannel = "current"
}
